import javax.xml.XMLConstants
import javax.xml.transform.stream.StreamSource
import javax.xml.validation.SchemaFactory

plugins {
    id 'com.android.application'
}

android {
    namespace 'com.sinux.pocketboard'
    compileSdk 36

    defaultConfig {
        applicationId "com.sinux.pocketboard"
        minSdk 28
        targetSdk 36
        versionCode 8
        versionName "1.1.6"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
}

dependencies {
    implementation 'com.google.android.material:material:1.13.0'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'androidx.autofill:autofill:1.3.0'
    implementation 'org.jetbrains:annotations:23.0.0'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'net.sf.kxml:kxml2:2.3.0'
}

task validateKeyboardMappings {
    dependsOn 'build'

    def factory = SchemaFactory.newInstance(XMLConstants.W3C_XML_SCHEMA_NS_URI)
    def schema = factory.newSchema(new File("${rootProject.projectDir}/app/keyboard-mapping.xsd"))
    def validator = schema.newValidator()

    File xmlRoot = new File("${rootProject.projectDir}/app/src/main/res/xml/")
    List<File> keyboardMappings = xmlRoot.listFiles(new FilenameFilter() {
        @Override
        boolean accept(File dir, String name) {
            return name.startsWith("keyboard_mapping_") && name.endsWith(".xml")
        }
    })

    for (File mappingFile : keyboardMappings) {
        validator.validate(new StreamSource(mappingFile))
    }
}